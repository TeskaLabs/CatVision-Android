apply plugin: 'com.android.library'

def getVersionCode = { ->
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'rev-list', '--first-parent', '--count', 'master'
            standardOutput = stdout
        }
        return Integer.parseInt(stdout.toString().trim())
    }
    catch (ignored) {
        return -1;
    }
}

def getVersionName = { ->
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'describe', '--abbrev=7', '--tag', '--dirty', '--always'
            standardOutput = stdout
        }
        return stdout.toString().trim()
    }
    catch (ignored) {
        return null;
    }
}

def getVersionNameDashes = { ->
    try {
        def vn = getVersionName()
        return vn.replace('.', '-')
    }
    catch (ignored) {
        return null;
    }
}

def getArchiveBaseName = { ->
    return "CatVision_Android_"+getVersionNameDashes()
}

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.3"

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 25
        versionCode getVersionCode()
        versionName getVersionName()
        setProperty("archivesBaseName", getArchiveBaseName())

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }
    sourceSets {
        main {
            jniLibs.srcDir 'src/main/libs'
            jni.srcDirs = []
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

task assembleWithSeaCat(dependsOn: assemble) << {

    android.libraryVariants.all { variant ->
        variant.outputs.each { output ->
            // Find SeaCat Client AAR
            def seacatAARPath = ""
            configurations.default.each {
                if (it.name.contains("SeaCat")) {
                    seacatAARPath = it.path
                }
            }
            if (seacatAARPath == "") {
                throw new GradleException('SeaCat AAR not found in project files.')
            }

            // Run merge script
            def process
            process = "${projectDir}/../merge_seacat_cvio_builds.sh ${seacatAARPath} ${output.outputFile.path}"
            process = process.execute()
            process.text.eachLine {println it}
            if (process.exitValue() != 0) {
                throw new GradleException("Execution of the merge script returned ${process.exitValue()}.")
            }
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile 'com.android.support:appcompat-v7:25.3.1'
    testCompile 'junit:junit:4.12'
    compile project(':seacat')
}
